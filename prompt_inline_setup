#!/bin/env zsh
source $^fpath/prompt_framework(N)

prompt_inline_setup() {
    setopt localoptions noshwordsplit prompt_subst

    # Load required modules
    zmodload zsh/datetime
    zmodload zsh/parameter

    unset PS1 PS2 RPS1

    prompt_clean_zstyles
    prompt_clean_add_hooks

    if [[ -n $2 ]]; then
        prompt_clean_pwd_length=$2
    else
        prompt_clean_pwd_length='1'
    fi

    local prompt_sym root_prompt_sym prompt_username term
    zstyle -s ':clean:normal:render' prompt-symbol prompt_sym || prompt_sym='â¯'
    zstyle -s ':clean:root:render' prompt-symbol root_prompt_sym || root_prompt_sym='#'
    if [[ -n $1 ]]; then
        [[ $1 =~ '256color$' ]]; term=$?
    else
        [[ $TERM =~ '256color$' ]]; term=$?
    fi

    # show username@host if logged in through SSH
    if [[ $SSH_CONNECTION != '' ]]; then
        if [[ $term -eq 0 ]]; then
            prompt_username='%F{83}%m%f:'
        else
            prompt_username='%F{10}%m%f:'
        fi
    fi
    # ( which rvm-prompt &> /dev/null ) && rvm_prompt='%F{242}$(rvm-prompt)%f'

    # Construct the new prompt with a clean preprompt.
    local -ah ps1 rps1
    if [[ $term -eq 0 ]]; then
        ps1=(
            $prompt_username
            '%F{45}%5v%f ' # Path
            "%F{%(?.207.203)}%(!.$root_prompt_sym.$prompt_sym)%f " # Prompt symbol
        )
        rps1=(
            '%(4V.%F{215}%4v%f .)' # Execution time
            '%(1V.%F{%(3V.83.242)}%1v%2v%(3V. %3v.)%f.)' # VCS status
        )
        PS2="%F{242}%_ %F{37}%(!.#.$prompt_sym)%f "
    else
        ps1=(
            $prompt_username
            '%F{14}%5v%f ' # Path
            "%F{%(?.13.9)}%(!.$root_prompt_sym.$prompt_sym)%f " # Prompt symbol
        )
        rps1=(
            '%(4V.%F{11}%4v%f .)' # Execution time
            '%(1V.%F{%(3V.10.8)}%1v%2v%(3V. %3v.)%f.)' # VCS status
        )
        PS2="%F{8}%_ %F{14}%(!.#.$prompt_sym)%f "
    fi

    PS1="${(j..)ps1}"
    RPS1="${(j..)rps1}"
}

prompt_inline_preview () {
    prompt_preview_theme inline
    prompt_preview_theme inline 256color
    prompt_preview_theme inline xterm 0
    prompt_preview_theme inline 256color 0
}

prompt_inline_setup "$@"

