#!/bin/env zsh
source $^fpath/prompt_clean_setup(N)

prompt_wordy_setup() {
    setopt localoptions noshwordsplit prompt_subst

    # Load required modules
    zmodload zsh/datetime
    zmodload zsh/parameter

    unset PS1 PS2 RPS1

    prompt_clean_add_hooks

    prompt_clean_zstyles
    zstyle ':clean:normal:*' prompt-symbol '$'

    zstyle ':vcs_info:*' unstagedstr '!'
    zstyle ':vcs_info:*' stagedstr '+'
    zstyle ':vcs_info:*:clean:*' untrackedstr '?'

    if [[ -n $2 ]]; then
        prompt_clean_pwd_length=$2
    else
        prompt_clean_pwd_length='3'
    fi

    local prompt_sym root_prompt_sym prompt_username term
    zstyle -s ':clean:normal:render' prompt-symbol prompt_sym || prompt_sym='❯'
    zstyle -s ':clean:root:render' prompt-symbol root_prompt_sym || root_prompt_sym='#'
    if [[ -n $1 ]]; then
        [[ $1 =~ '256color$' ]]; term=$?
    else
        [[ $TERM =~ '256color$' ]]; term=$?
    fi

    # show username@host if logged in through SSH
    if [[ $SSH_CONNECTION != '' ]]; then
        if [[ $term -eq 0 ]]; then
            prompt_username='%F{83}%m%f:'
        else
            prompt_username='%F{10}%m%f:'
        fi
    fi
    # ( which rvm-prompt &> /dev/null ) && rvm_prompt='%F{242}$(rvm-prompt)%f'

    # Construct the new prompt with a clean preprompt.
    local -ah ps1
    if [[ $term -eq 0 ]]; then
        ps1=(
            $prompt_newline # Initial newline, for spaciousness.
            $prompt_username
            '%B%F{45}%5v%b%f' # Path
            '%(1V. on %F{207}⎇ %1v%f%(2v. %B%F{203}[%2v]%b%f.)%(3V. %F{242}%3v.)%f.)' # VCS status
            '%(4V. %F{215}%4v%f.)' # Execution time
            $prompt_newline # Separate preprompt and prompt.
            "%F{%(?.41.203)}%(!.$root_prompt_sym.$prompt_sym)%f " # Prompt symbol
        )
        PS2="%F{242}%_ %F{37}%(!.#.$prompt_sym)%f "
    else
        ps1=(
            $prompt_newline # Initial newline, for spaciousness.
            $prompt_username
            '%F{14}%5v%f' # Path
            '%(1V. on %F{13}%1v%(2v. %F{9}[%2v].)%(3V. %F{10}%3v.)%f.)' # VCS status
            '%(4V. %F{11}%4v%f.)' # Execution time
            $prompt_newline # Separate preprompt and prompt.
            "%F{%(?.2.9)}%(!.$root_prompt_sym.$prompt_sym)%f " # Prompt symbol
        )
        PS2="%F{8}%_ %F{14}%(!.#.$prompt_sym)%f "
    fi

    PS1="${(j..)ps1}"
}
prompt_wordy_preview () {
    prompt_preview_theme wordy "$@"
}

prompt_wordy_setup "$@"
