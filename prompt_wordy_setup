#!/bin/env zsh
source $^fpath/prompt_framework(N)

prompt_wordy_setup() {
    setopt localoptions noshwordsplit prompt_subst

    # Load required modules
    zmodload zsh/datetime
    zmodload zsh/parameter

    unset PS1 PS2 RPS1

    prompt_clean_add_hooks

    prompt_clean_zstyles
    zstyle ':clean:normal:*' prompt-symbol '$'

    zstyle ':vcs_info:*' unstagedstr '!'
    zstyle ':vcs_info:*' stagedstr '+'
    zstyle ':vcs_info:*:clean:*' untrackedstr '?'

    if [[ -n $1 ]]; then
        prompt_clean_pwd_length=$1
    else
        prompt_clean_pwd_length='3'
    fi

    local prompt_sym root_prompt_sym prompt_username term
    zstyle -s ':clean:normal:render' prompt-symbol prompt_sym || prompt_sym='â¯'
    zstyle -s ':clean:root:render' prompt-symbol root_prompt_sym || root_prompt_sym='#'

    # show username@host if logged in through SSH
    if [[ $SSH_CONNECTION != '' ]]; then
        prompt_username='%F{10}%m%f:'
    fi
    # ( which rvm-prompt &> /dev/null ) && rvm_prompt='%F{242}$(rvm-prompt)%f'

    # Construct the new prompt with a clean preprompt.
    local -ah ps1
    ps1=(
        $prompt_newline # Initial newline, for spaciousness.
        $prompt_username
        '%F{14}%5v%f' # Path
        '%(1V. on %F{13}%1v%(2V. %F{9}[%2v].)%(3V. %F{10}%3v.)%f.)' # VCS status
        '%(4V. %F{11}%4v%f.)' # Execution time
        $prompt_newline # Separate preprompt and prompt.
        "%F{%(?.2.9)}%(!.$root_prompt_sym.$prompt_sym)%f " # Prompt symbol
    )
    PS2="%F{8}%_ %F{14}%(!.#.$prompt_sym)%f "

    PS1="${(j..)ps1}"
}
prompt_wordy_preview () {
    prompt_preview_theme wordy "$@"
}

prompt_wordy_setup "$@"
